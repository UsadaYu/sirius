add_subdirectory(cmake)

set(_artifact_path "${CMAKE_CURRENT_BINARY_DIR}/artifact")
set(_artifact_inc_path "${_artifact_path}/include")
set(_artifact_bin_path "${_artifact_path}/bin")

file(GLOB _header_list "${PROJECT_SOURCE_DIR}/include/sirius/*.h")
file(COPY ${_header_list} DESTINATION "${_artifact_inc_path}/sirius")

file(GLOB _header_custom_list "${PROJECT_SOURCE_DIR}/include/sirius/custom/*.h")
file(COPY ${_header_custom_list} DESTINATION "${_artifact_inc_path}/sirius/custom")

set(_src_dir "${CMAKE_CURRENT_SOURCE_DIR}/src")
file(GLOB _src_list "${_src_dir}/*.cpp" "${_src_dir}/*.c")

list(
  APPEND _link_dir_list
  ${TARGET_LIB_DIR}
  ${SIRIUS_TEST_EXTRA_LINK_DIR}
)

list(
  APPEND _link_libs_list
  ${SIRIUS_TARGET_NAME}
  ${SIRIUS_TEST_EXTRA_LINK_LIBRARIES}
)

foreach(file ${_src_list})
  get_filename_component(file_name ${file} NAME_WE)
  set(_target_name "${SIRIUS_TARGET_NAME}_${file_name}")

  add_executable(${_target_name} ${file})
  set_target_properties(
    ${_target_name}
    PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${_artifact_bin_path}
  )

  target_include_directories(
    ${_target_name}
    PRIVATE ${_artifact_inc_path}
  )

  foreach(path ${_link_dir_list})
    target_link_directories(${_target_name} PRIVATE ${path})
  endforeach()

  target_link_libraries(${_target_name} ${_link_libs_list})

  if(NOT CMAKE_SYSTEM_NAME STREQUAL "Windows")
    if(${SIRIUS_TEST_PIE_ENABLE})
      target_compile_options(${_target_name} PRIVATE -fPIE)
      target_link_options(${_target_name} PRIVATE -pie)
    else()
      target_link_options(${_target_name} PRIVATE -no-pie)
    endif()
  endif()

  if(${CMAKE_CXX_COMPILER_ID} STREQUAL "Clang" OR
    ${CMAKE_CXX_COMPILER_ID} STREQUAL "GNU")
    target_compile_options(${_target_name} PRIVATE -Wall)
  elseif(${CMAKE_CXX_COMPILER_ID} STREQUAL "MSVC")
    target_compile_options(${_target_name} PRIVATE /W4)
  endif()

  target_compile_definitions(
    ${_target_name}
    PRIVATE -Dlog_module_name="${SIRIUS_TEST_MODULE_PRINT_NAME}"
  )

  target_compile_definitions(
    ${_target_name}
    PRIVATE -Dexternal_log_level=${SIRIUS_TEST_EXTERNAL_LOG_LEVEL}
  )

  add_test(
    NAME ${_target_name}
    COMMAND ./${_target_name}
    WORKING_DIRECTORY ${_artifact_bin_path}
  )
endforeach()

add_custom_target(
  custom_clean
  COMMAND ${CMAKE_COMMAND} -E remove ${_artifact_bin_path}/test*_*.*
  COMMENT "delete the files generated by the test"
)
